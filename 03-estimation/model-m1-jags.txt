model {
  
  ## likelihood ----
  
  for (i in 1:D[1]) {
    for (j in 1:D[2]) {
    
      logit(pi[i, j]) <- delta[j] + lambda[j] * xstar[i] - beta[j] * xstar[i] ^ 2
      y_ideo[i, j] ~ dbern(pi[i, j])
      
    }
  }
  
  
  ## priors ----
  
  for (i in 1:D[1]) {
  
    xstar[i] ~ dnorm(0, 1)

  }

  for (j in 1:D[2]) {
  
    delta[j] ~ dnorm(0, 0.2)
    lambda[j] ~ dnorm(0, 0.2)
    beta[j] ~ dlnorm(0, 0.5)
    
  }
  
  
  ## transformation and identification of posterior draws ----
  
  xbar <- mean(xstar)
  sdx <- sd(xstar)
  ocon <- lambda[con] / (2 * beta[con])
  polarity <- ifelse(xbar < ocon, 1, -1)

  for (i in 1:D[1]) {
  
    x[i] <- polarity * (xstar[i] - xbar) / sdx

  }
  
  for (j in 1:D[2]) {
  
    a[j] <- delta[j] + beta[j] * (lambda[j] / (2 * beta[j])) ^ 2
    b[j] <- beta[j] * sdx ^ 2
    o[j] <- polarity * ((lambda[j] / (2 * beta[j])) - xbar) / sdx

  }

}
